class Main
{   
    Description = "Survive multiple waves of titans.";
    StartTitans = 3;
    AddTitansPerWave = 1;
    MaxWaves = 20;
    RespawnOnWave = true;
    GradualSpawn = true;
    GradualSpawnTooltip = "Spawn new titans gradually over time. Helpful for reducing lag.";
    ChallengeWaves = "Punks";
    ChallengeWavesTooltip = "Makes every Nth wave spawn special titans";
    ChallengeWavesDropbox = "Disabled, Punks, Throwers, Crawlers, Random";
    ChallengeWavesEveryX = 5;

    _currentWave = 0;
    _hasSpawned = false;

    function OnGameStart()
    {
        if (Network.IsMasterClient)
        {
            self.NextWave();
        }
    }

    function OnCharacterSpawn(character)
    {
        if (character.IsMine && character.Type == "Titan")
        {
            if (character.DetectRange > 0)
            {
                character.DetectRange = 2000;
            }
        }
    }

    function OnTick()
    {
        if (Network.IsMasterClient && !Game.IsEnding)
        {
            titans = Game.Titans.Count;
            humans = Game.Humans.Count;
		    playerShifters = Game.PlayerShifters.Count;
            if (humans > 0 || playerShifters > 0)
            {
                self._hasSpawned = true;
            }
            if (titans == 0)
            {
                self.NextWave();
            }
            if (humans == 0 && playerShifters == 0 && self._hasSpawned)
            {
                UI.SetLabelAll(UILabelEnum.MiddleCenter, "Humanity failed!");
                Game.End(10.0);
                return;
            }
            UI.SetLabelAll(UILabelEnum.TopCenter, "Titans Left: " + Convert.ToString(titans) + "  " + "Wave: " + Convert.ToString(self._currentWave));
        }
    }

    function NextWave()
    {
        self._currentWave = self._currentWave + 1;
        if (self._currentWave > self.MaxWaves)
        {
            UI.SetLabelAll(UILabelEnum.MiddleCenter, "All waves cleared, humanity wins!");
            Game.End(10.0);
            return;
        }
        amount = self.AddTitansPerWave * (self._currentWave - 1) + self.StartTitans;
        type = TitanTypeEnum.Default;
        if (self.ChallengeWaves != "Disabled" && self.ChallengeWavesEveryX > 0 && Math.Mod(self._currentWave, self.ChallengeWavesEveryX) == 0)
        {
            amount = self._currentWave / self.ChallengeWavesEveryX;
            if (self.ChallengeWaves == "Punks")
            {
                type = TitanTypeEnum.Punk;
            }
            elif (self.ChallengeWaves == "Throwers")
            {
                type = TitanTypeEnum.Thrower;
            }
            elif (self.ChallengeWaves == "Crawlers")
            {
                type = TitanTypeEnum.Crawler;
            }
            elif (self.ChallengeWaves == "Random")
            {
                type = TitanTypeEnum.Random;
            }
        }
        if (self.GradualSpawn)
        {
            Game.SpawnTitansAsync(type, amount);
        }
        else
        {
            Game.SpawnTitans(type, amount);
        }
        if (self.RespawnOnWave)
        {
            Game.SpawnPlayerAll(false);
        }
    }
}
